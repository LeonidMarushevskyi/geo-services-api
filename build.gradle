import static org.gradle.api.logging.LogLevel.*
import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
        classpath 'se.transmode.gradle:gradle-docker:1.2'
        classpath 'com.github.sedovalx.gradle:gradle-aspectj-binary:1.0.24'
    }
}

plugins {
    id "org.sonarqube" version "2.5"
    id 'net.ltgt.apt' version '0.9'
    id "net.foragerr.jmeter" version "1.0.7-3.0-BETA"
    id "com.github.hierynomus.license" version "0.14.0"
}

project.logging.setLevelInternal(INFO)

project.ext {
    projectGroup = 'gov.ca.cwds.geo'
    projectDockerGroup = 'cwds'
    projectDockerAppName = 'geo-services-api'
    projectMajorVersion = '0.7.5'

    isRelease = System.getProperty("Release") == "true"
    buildNumber = System.getProperty('BuildNumber')
    customVersion = System.getProperty('CustomVersion')

    projectSnapshotVersion = projectMajorVersion + "-SNAPSHOT"
    projectReleaseVersion = (customVersion == null || customVersion == "" || customVersion.startsWith('$') ? projectMajorVersion + '_' + buildNumber + '-RC' : customVersion )
    projectVersion = (isRelease ? projectReleaseVersion : projectSnapshotVersion )
    dockerTag = projectVersion

    dropwizardVersion = '1.1.0'
    coreApiVersion = '1.9.4_867-RC'
    mapstructVersion = '1.1.0.Final'
    mainClass = 'gov.ca.cwds.geo.GeoServicesApiApplication'

    // Publishing configuration
    snapshotMavenRepo = "http://sonar.dev.cwds.io:8081/artifactory/repo"
    releaseMavenRepo = "http://sonar.dev.cwds.io:8081/artifactory/libs-release"
    artifactoryRepo = ( isRelease ? releaseMavenRepo : snapshotMavenRepo)
    
}

group projectGroup
if (System.getProperty('newVersion')) {
      def newVersion = "${System.getProperty('newVersion')}"
      version = newVersion
      projectVersion = newVersion
    } else {
      version = projectVersion
}

repositories {
    mavenLocal()
    maven {
        url "http://sonar.dev.cwds.io:8081/artifactory/repo"
    }
}

apply plugin: "jacoco"
apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'docker'
apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'maven-publish'
apply plugin: 'com.github.sedovalx.gradle-aspectj-binary'

weaveClasses.dependsOn compileJava
classes.dependsOn weaveClasses


sonarqube {
    properties {
        property "sonar.projectName", "${name}"
        property "sonar.projectKey", "${group}:${name}"
        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec"
    }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${project.buildDir}/jacocoHtml"
    }
    
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: [
                    'org/hibernate/*'
            ])
        })
    }
}


jmeter {
    jmLog = file("${project.buildDir}/jmeter/jmeter.log")
}


configurations.all {
    // Check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {

    compile fileTree(dir: 'lib', include: ['*.jar'])

// Dropwizard-guice 1.0.0.2 conflicts with jersey-client 2.25.
// Exclude transitive dependencies.

    compile (group: 'gov.ca.cwds.api', name: 'api-core-rest', version: coreApiVersion , changing: true) {
        exclude group: 'org.glassfish.jersey.core'
    }

    compile group: "com.jcabi", name: "jcabi-aspects", version: "0.22.6"
    compile group: "com.smartystreets.api", name: "smartystreets-java-sdk", version: "3.2.0"
    compile group: 'com.javadocmd', name: 'simplelatlng', version: '1.3.0'
    compile group: 'org.bouncycastle', name: 'bcprov-jdk16', version: '1.46'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.1'
    compile group: 'org.slf4j', name: 'slf4j-ext', version: '1.7.25'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

    compile (
            'io.dropwizard:dropwizard-core:' + dropwizardVersion,
            'io.dropwizard:dropwizard-testing:' + dropwizardVersion
    )
    compile 'org.mapstruct:mapstruct-jdk8:' + mapstructVersion
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.1'
    compile group: 'org.apache.commons', name: 'commons-compress', version: '1.14'
    compile group: 'commons-io', name: 'commons-io', version: '2.5'

    testCompile group: 'org.skyscreamer', name: 'jsonassert', version: '1.5.0'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'org.mockito:mockito-core:2.21.0'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'
    testCompile group: 'com.h2database', name: 'h2', version: '1.4.194'
    testCompile('org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.23.2') {
        exclude group: 'javax.servlet', module: 'javax.servlet-api'
        exclude group: 'junit', module: 'junit'
    }

    apt 'org.mapstruct:mapstruct-processor:' + mapstructVersion
}

mainClassName = mainClass

shadowJar {
    classifier = 'dist'
    baseName = 'geo-services-api'
    mergeServiceFiles()
    manifest {
        attributes 'Main-Class': mainClassName
    }
    version = ''
}

run {
    args 'server', './config/geo-services-api.yml'
}

test {
    systemProperty 'java.net.preferIPv4Stack', 'true'
    exclude '**/*IT.*'
}

jar {
    manifest {
        attributes 'Main-Class': mainClassName
    }
}

task readArguments() {
    doLast{
        println "Release: " + isRelease
        println "Build: " + buildNumber
        println "Docker Tag: " + dockerTag
    }
}

task integrationTest(type: Test, dependsOn: testClasses) {

    // URL where integration tests will be pointed to (may change it in gradle.properties)
    systemProperty 'geoservices.api.url', project.property('geoservices.api.url')

}

task copyConfig(type: Copy) {
    from 'config'
    into 'build/docker/config'
}

task copyDist(type: Copy, dependsOn: shadowJar) {
    from 'build/libs'
    into 'build/docker/libs'
}


task geoServicesApiDocker(type: Docker,  dependsOn: [copyConfig, copyDist] ) {
    dockerfile = "Dockerfile"
    project.group = projectDockerGroup
    applicationName = projectDockerAppName
    tagVersion = dockerTag
}

task createDockerImage(type:Exec, dependsOn: shadowJar) {
    commandLine 'docker',  'build',  '-t',  'cwds/geo-services-api', '.'
}

task tagDockerImage(type:Exec, dependsOn: createDockerImage) {
    commandLine 'docker',  'tag',  'cwds/geo-services-api',  "cwds/geo-services-api:$dockerTag"
}

task pushDockerLatest(type:Exec, dependsOn: createDockerImage) {
    commandLine 'docker',  'push',  'cwds/geo-services-api:latest'
}

task pushDockerVersionTagged(type:Exec, dependsOn: tagDockerImage) {
    commandLine 'docker',  'push',  "cwds/geo-services-api:$dockerTag"
}

task dropDockerImage(type:Exec) {
    commandLine 'docker',  'rmi',  "cwds/geo-services-api:$dockerTag"
    commandLine 'docker',  'rmi',  "cwds/geo-services-api:latest"

}
task publishDocker(dependsOn: [pushDockerVersionTagged, pushDockerLatest]) {
    doLast {
        println 'Published'
    }
}

task createGitTag(type:Exec) {
    commandLine "git", "tag", "$dockerTag"
}

task pushGitTag(type:Exec, dependsOn: createGitTag) {
    doFirst{
        environment "GIT_SSH_COMMAND", 'ssh -o UserKnownHostsFile=/dev/null -o StrictHostKeyChecking=no'
    }
    
    commandLine "git", "push", "origin", "$dockerTag"
    
    doLast {
        println "Pushed tagged to Git origin"
    }
}

processResources {
    filter(ReplaceTokens, tokens:[
            'build.version' : projectVersion,
            'build.number' : (System.getenv("BUILD_NUMBER") ?: "IDE")
    ])
}

publishing {
    publications {
        library(MavenPublication) {
            from components.java
            groupId projectGroup
            artifactId rootProject.name
        }
    }
    repositories {
        maven {
            url artifactoryRepo
        }
    }
}
